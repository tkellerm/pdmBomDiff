..!interpreter english translate noabbrev
..*****************************************************************************
.. Name           : PDMBOMDIFF.BA
.. Author         : cl
.. Verantwortlich : cl
.. Kontrolle      : fwester
.. Functionality  : Creates a new productions list.
..*****************************************************************************
.set trans -
.type text  xpdmzid     ? _F|defined(U|xpdmzid)
.type text  xpdmzidstd  ? _F|defined(U|xpdmzidstd)
.type text  xanzahl     ? _F|defined(U|xanzahl)
.type text  xlge        ? _F|defined(U|xlge)
.type text  xbreite     ? _F|defined(U|xbreite)
.type text  xanzahlstd  ? _F|defined(U|xanzahlstd)
.type text  xlgestd     ? _F|defined(U|xlgestd)
.type text  xbreitestd  ? _F|defined(U|xbreitestd)
.type text  xdontchange ? _F|defined(U|xdontchange)
.type DVM2  xtmpfile    ? _F|defined(U|xtmpfile)
.type text  xtout       ? _F|defined(U|xtout)
.type text  xterr       ? _F|defined(U|xterr)
.type text  xnutzen     ? _F|defined(U|xnutzen)
.type text  xpverlust   ? _F|defined(U|xpverlust)
.type text  xvkpverlust ? _F|defined(U|xvkpverlust)
.type text  xamge       ? _F|defined(U|xamge)
..
.. Initialize
.file -tempname U|xtmpfile
.file -tempname U|xtout
.file -tempname U|xterr
..
.continue ARTIKEL ? G|evtartengl = "BUTTON_AFTER"  & G|evtvar = "bneuestdfl"
.continue DECLARE ? G|evtartengl = "BUTTON_AFTER"  & G|evtvar = "bneuefl"
.continue END
..
!ARTIKEL
.continue DECLARE ?  M|noloesch
.. Artikel �ndern, Standardfertigungsliste l�schen
.output new 'U|xtmpfile'
.input "DATEI.F"
#!database=2:1
#!action=update
#!CHARSET=EKS
id;flistestd
'M|artikel^id';;
.output
.system 'F|formatstr("edpimport.sh -p {1} -I {2} >{3} 2>{4}", G|einmalpw, U|xtmpfile, U|xtout, U|xterr)' background
.continue ERROR02 ? _G|mehr
.continue PARTSUCCESS
..
!DECLARE: Declarations
.formula U|xdontchange = "~~~~~~~~~~"
.formula M|suchneu = M|flistestd^such ? F|empty(M|suchneu)
..
.select screen header
..
.. EDP header
.output new 'U|xtmpfile'
.input "DATEI.F"
# Fertigungsliste neu
#!database=128
#!group=1
#!action=new
#!CHARSET=EKS
#!DONTCHANGE='U|xdontchange'
#!EDP_FOPMODE=0
#!EDP_UPDATEMODE=ERRABORT
..
?artikel;such;flistestd;lgruppe\   ? _F|empty(M|suchneu) & G|evtvar = "bneuestdfl"
?artikel;such;lgruppe\             ? _F|empty(M|suchneu) & G|evtvar = "bneuefl"
..
elex;pdmposnr;pdmzid;vpos;lge;breite;anzahl;nutzen;bu;filter;ptext;pverlust;vkpverlust;amge;var;pzeit;tlzeit;tersatzt;tverschlt;tnwpflicht;tserarchiv;ybelast;
..
?'M|artikel^id';'M|suchneu';true;'M|lgruppe^id'\ ? G|evtvar = "bneuestdfl"
?'M|artikel^id';'M|suchneu';'M|lgruppe^id'\      ? G|evtvar = "bneuefl"
.output
..
!LINE
.formula U|xlge       = U|xdontchange
.formula U|xbreite    = U|xdontchange
.formula U|xanzahl    = U|xdontchange
.formula U|xlgestd    = U|xdontchange
.formula U|xbreitestd = U|xdontchange
.formula U|xanzahlstd = U|xdontchange
..
.select screen line
.continue EXECUTE ? _G|mehr
.continue LINE    ? M|loesch
.assign U|xpdmzid     = M|pdmzid
.formula U|xlge       = M|lge        ? _F|empty(M|lge)
.formula U|xbreite    = M|breite     ? _F|empty(M|breite)
.formula U|xanzahl    = M|anzahl     ? _F|empty(M|anzahl)
.assign U|xpdmzidstd  = M|stdpdmzid
.formula U|xlgestd    = M|lgestd     ? _F|empty(M|lgestd)
.formula U|xbreitestd = M|breitestd  ? _F|empty(M|breitestd)
.formula U|xanzahlstd = M|anzahlstd  ? _F|empty(M|anzahlstd)
..
.continue DOSEL1 ? M|sel1
.continue DOSEL2 ? M|sel2
..
.continue LINE
..
!DOSEL1
.formula U|xnutzen     = M|nutzenstd
.formula U|xnutzen     = U|xdontchange   ? _F|empty(M|elexstd) & M|elexstd^gruppe = 2
.formula U|xpverlust   = M|pverluststd
.formula U|xpverlust   = U|xdontchange   ? _F|empty(M|elexstd) & M|elexstd^gruppe = 2
.formula U|xvkpverlust = M|vkpverluststd
.formula U|xvkpverlust = U|xdontchange   ? _F|empty(M|elexstd) & M|elexstd^gruppe = 2
.formula U|xamge       = M|amgestd
.formula U|xamge       = U|xdontchange   ? _F|empty(M|elexstd) & M|elexstd^gruppe = 2
..
.continue EDPLINE
..
!DOSEL2
.formula U|xnutzen     = M|nutzen
.formula U|xnutzen     = U|xdontchange ? _F|empty(M|elex) & M|elex^gruppe = 2
.formula U|xpverlust   = M|pverlust
.formula U|xpverlust   = U|xdontchange ? _F|empty(M|elex) & M|elex^gruppe = 2
.formula U|xvkpverlust = M|vkpverlust
.formula U|xvkpverlust = U|xdontchange ? _F|empty(M|elex) & M|elex^gruppe = 2
.formula U|xamge       = M|amge
.formula U|xamge       = U|xdontchange ? _F|empty(M|elex) & M|elex^gruppe = 2
..
!EDPLINE
..
.output addedto 'U|xtmpfile'
..
?'M|elex^id';'M|pdmpos';'U|xpdmzid';'M|vpos';'U|xlge';'U|xbreite';'M|anzahl';'U|xnutzen';'M|bu';'M|filter';'M|ptext';'U|xpverlust';'U|xvkpverlust';'U|xamge';'M|var';'M|pzeit';'M|tlzeit';'M|tersatz';'M|tverschlt';'M|tnwpflicht';'M|tserarchiv';'M|ybelast';\ ? M|sel2
?'M|elexstd^id';'M|stdpdmpos';'U|xpdmzidstd';'M|vposstd';'U|xlgestd';'U|xbreitestd';'U|xanzahlstd';'U|xnutzen';'M|bustd';'M|filterstd';'M|ptextstd';'U|xpverlust';'U|xvkpverlust';'U|xamge';'M|varstd';'M|pzeitstd';'M|tlzeitstd';'M|tersatzstd';'M|tverschltstd';'M|tnwpflichtstd';'M|tserarchivstd';'M|ybelaststd';\ ? M|sel1
..
.continue LINE
..
!EXECUTE: Execute import
.output
.system 'F|formatstr("edpimport.sh -p {1} -I {2} >{3} 2>{4}", G|einmalpw, U|xtmpfile, U|xtout, U|xterr)' background
.continue ERROR01 ? _G|mehr
.continue SUCCESS
..*****************************************************************************
!END
.continue
..*****************************************************************************
.set trans +
..
!SUCCESS: Fertigungsliste erfolgreich erstellt
.input "is/TABLEEREN"
.continue STDSUCCESS ? G|evtvar = "bneuestdfl"
.note Die neue Fertigungsliste wurde angelegt.
.continue CLRNOSTD
..
!STDSUCCESS: Standardfertigungsliste erfolgreich erstellt
.note Die neue Standardfertigungsliste wurde angelegt.
.continue CLRSTD
..
!PARTSUCCESS
.note Standardfertigungsliste 'M|flistestd^nummer' aus Artikel erfolgreich entfernt!
.input "is/TABLEEREN"
..
!CLRSTD
.formula M|flistestd = ""
.formula M|nrflistestd = ""
.formula M|flbasisstd = 0
.continue END ? G|evtvar = "bneuestdfl"
..
!CLRNOSTD
.formula M|fliste = ""
.formula M|nrfliste = ""
.formula M|flbasis = 0
.continue END
..
!ERROR01: Import failed!
.error -line 0 -field M|bneuefl -message Das Erstellen der Fertigungsliste ist gescheitert! ? G|evtvar = "bneuefl"
.error -line 0 -field M|bneuestdfl -message Das Erstellen der Standardfertigungsliste ist gescheitert! ? G|evtvar = "bneuestdfl"
.output terminal
Fehlerdatei:
.input 'U|xtout'
.input 'U|xterr'
.end 1
..
!ERROR02: Import failed!
.error -message Das L�schen der Standardfertigungsliste im Artikel 'M|artikel' ist gescheitert!
.output terminal
..
!ERROR
Fehlerdatei:
.input 'U|xtout'
.input 'U|xterr'
.end 1
